"Name" = Nival A7 Shader Script
"Author" = Yuri V. Blazhevich
"Version" = 1.0.0.0

"Case Sensitive" = False
"Start Symbol" = <Declarations>

{Hex Digit}    = {Digit} + [abcdefABCDEF]

! Id = {Letter}{Alphanumeric}*
Comment Line    = '//'
! FloatLiteral  = {Digit}*.{Digit}+
DecimalLiteral  = {Digit}+
HexLiteral      = 0x{Hex Digit}+

<Declarations>          ::= <Declaration> <Declarations>
			|
                         
<Declaration>		::= <Technique>

<Technique>		::= technique '(' <Num Textures> ',' <Num Stages> ')'  '{' <Technique Def> '}'
			| technique '(' <Num Textures> ',' <Num Stages> ',' <Stencil> ')'  '{' <Technique Def> '}'

<Num Textures>		::= DecimalLiteral

<Num Stages>		::= DecimalLiteral

<Stencil>		::= DecimalLiteral

<Technique Def>		::= <Set Block>
			| <Set Block> <Restore Block>

<Set Block>		::= set '{' <Defs Block> '}'

<Restore Block>		::= restore '{' <Defs Block> '}'

<Defs Block>		::= <Color Ops> <Alpha Ops>
			| <Color Ops> <Alpha Ops> <Properties>

<Color Ops>		::= color '=' <Expression>
			|

<Alpha Ops>		::= alpha '=' <Expression>
			|

<Func Args 1>		::= '(' <Expression> ')'
<Func Args 2>		::= '(' <Expression> ',' <Expression> ')'
<Func Args 3>		::= '(' <Expression> ',' <Expression> ',' <Expression> ')'

<Expression>		::= <Op Add>

<Exp>			::= Replicate <Func Args 1>
			| Complement <Func Args 1>
			| Add <Func Args 2>			
			| AddSmooth <Func Args 2>
			| AddSigned <Func Args 2>
			| AddSigned2x <Func Args 2>
			| Sub <Func Args 2>
			| Mul <Func Args 2>
			| Mul2x <Func Args 2>
			| Mul4x <Func Args 2>
			| Lerp <Func Args 3>
			| Mad <Func Args 3>
			| <Arg>

<Op Add>     		::= <Op Add> '+' <Op Mult>
               		| <Op Add> '-' <Op Mult>
               		| <Op Mult>

<Op Mult>    		::= <Op Mult> '*' <Op Other>
               		| <Op Other>

<Op Other>		::= '(' <Expression> ')'
			| <Exp>

<Arg>			::= <Texture>
			| <Diffuse Color>
			| <Specular Color>
			| <TFactor>

<Diffuse Color>		::= cdf
<Specular Color>	::= csp
<TFactor>		::= tfactor
<Texture>		::= tex '(' <Int Val> ')'


<Properties>		::= <Props Block>
			| <Texture Props Block>
			| <Props Block> <Texture Props Block>
			| <Texture Props Block> <Props Block>

<Props Block>		::= props '{' <PropsList> '}'

<PropsList>		::= <PropSingle>
			| <PropSingle> <PropsList>

<PropSingle>		::= <Prop Blend>
			| <Prop Depth>
			| <Prop Cull>
			| <Prop AlphaTest>
			| <Prop Specular>
			| <Prop Stencil>
			| <Prop TFactor>
			| <Prop Clipping>
			| <Prop Lighting>
			| <Prop Ambient>

<Cmp Func>		::= <Cmp Func Cmp>
			| <Cmp Func NoCmp>

<Cmp Func Cmp>		::= Less
			| LessEqual
			| Greater
			| GreaterEqual
			| Equal
			| NotEqual

<Cmp Func NoCmp>	::= Never
			| Always

<Prop Ambient>		::= ambient '=' <Int Val>

<Prop Lighting>		::= lighting '=' <Bool Val>

<Prop Clipping>		::= clipping '=' <Bool Val>

<Prop TFactor>		::= tfactor = <Int Val>

<Prop Stencil>		::= stencil '=' <Stencil Args> do '{' <Stencil Actions> '}'
			| stencil '=' <Stencil Args>

<Stencil Args>		::= <Cmp Func Cmp> '(' <Stencil Ref Val> ',' <Stencil Mask> ')'
			| <Cmp Func Cmp> '(' <Stencil Ref Val> ')'
			| <Cmp Func NoCmp>
			| None

<Stencil Ref Val>	::= DecimalLiteral
			| HexLiteral

<Stencil Mask>		::= DecimalLiteral
			| HexLiteral

<Stencil Actions>	::= <Stencil Action> ',' <Stencil Action> ',' <Stencil Action>

<Stencil Action>	::= Keep
			| Zero
			| Replace
			| Inc
			| Dec
			| IncSat
			| DecSat
			| Invert

<Prop Specular>		::= specular '=' <Bool Val>

<Prop AlphaTest>	::= alphatest '=' <AlphaTest Vals>

<AlphaTest Vals>	::= None
			| <Cmp Func Cmp> '(' <Alpha Ref> ')'
			| <Cmp Func NoCmp>
			
<Alpha Ref>		::= DecimalLiteral

<Prop Cull>		::= cull '=' <Cull Mode>

<Cull Mode>		::= None
			| CW
			| CCW

<Prop Depth>		::= depth '=' <Depth Func>

<Depth Func>		::= None
			| NoWrite
			| <Cmp Func>

<Prop Blend>		::= blend '=' <Blend Op> '(' <Src Blend> ',' <Dst Blend> ')'

<Blend Op>		::= Add
			| Sub
			| RevSub
			| Min
			| Max

<Src Blend>		::= <Blend Modes>

<Dst Blend>		::= <Blend Modes>

<Blend Modes>		::= Zero
			| One
			| SrcColor
			| InvSrcColor
			| SrcAlpha
			| InvSrcAlpha
			| DstColor
			| InvDstColor
			| DstAlpha
			| InvDstAlpha
			| SrcAlphaSat

<Texture Props Block>	::= <TexStageProps>
			| <TexStageProps> <Texture Props Block>

<TexStageProps>		::= texprops '(' <Int Val> ')' '{' <TexPropList> '}'

<TexPropList>		::= <TexSingleProp>
			| <TexSingleProp> <TexPropList>

<TexSingleProp>		::= address '(' <TexWrapMode> ',' <TexWrapMode> ',' <TexWrapMode> ')'
			| address '(' <TexWrapMode> ',' <TexWrapMode> ')'
			| filter '(' <TexFilterMode> ',' <TexFilterMode> ',' <TexFilterMode> ')'
			| filter '(' <TexFilterMode> ',' <TexFilterMode> ')'

<TexWrapMode>		::= Wrap
			| Mirror
			| Clamp
			| Border
			| MirrorOnce

<TexFilterMode>		::= None
			| Point
			| Linear
			| Anisotropic
			| FlatCubic
			| GaussianCubic

<Int Val>		::= HexLiteral
			| DecimalLiteral

<Bool Val>		::= true
			| false

