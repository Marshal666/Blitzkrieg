"Name" = Nival A7 Shader Script
"Author" = Yuri V. Blazhevich
"Version" = 1.0.0.0

"Case Sensitive" = False
"Start Symbol" = <Declarations>

{Hex Digit}    = {Digit} + [abcdefABCDEF]

! Id = {Letter}{Alphanumeric}*
Comment Line    = '//'
! FloatLiteral  = {Digit}*.{Digit}+
DecimalLiteral  = {Digit}+
HexLiteral      = 0x{Hex Digit}+

<Declarations>			::= <Declaration> <Declarations>
						|
                         
<Declaration>			::= <Technique>

<Technique>				::= technique '(' <Num Textures> ',' <Num Stages> ')'  '{' <Technique Def> '}'
						| technique '(' <Num Textures> ',' <Num Stages> ',' <Stencil> ')'  '{' <Technique Def> '}'

<Num Textures>			::= DecimalLiteral

<Num Stages>			::= DecimalLiteral

<Stencil>				::= DecimalLiteral

<Technique Def>			::= <Set Block>
						| <Set Block> <Restore Block>

<Set Block>				::= set '{' <Defs Block> '}'

<Restore Block>			::= restore '{' <Defs Block> '}'

<Defs Block>			::= <Color Ops> <Alpha Ops>
						| <Color Ops> <Alpha Ops> <Properties>

<Color Ops>				::= color '=' <Expression> ';'
						|

<Alpha Ops>				::= alpha '=' <Expression> ';'
						|

<Func Args 2>			::= '(' <Arg> ',' <Arg> ')'
<Func Args 3>			::= '(' <Arg> ',' <Arg> ',' <Arg> ')'

<Expression>			::= <Exp>
						| <Expression> '|' <Exp>

<Exp>					::= <Exp2> <Func Args 2>
						| <Exp3> <Func Args 3>
						| <Arg>                        

<Exp2>					::= Add
						| AddSmooth
						| AddSigned
						| AddSigned2x
						| Sub
						| Mul
						| Mul2x
						| Mul4x
						| BlendCurrentAlpha
						| BlendTextureAlpha
						| BlendDiffuseAlpha
						| BlendFactorAlpha
						| DP3

<Exp3>					::= Lerp
						| Mad

<SimpleArg>				::= cdf
						| csp
						| tfactor
						| curr
						| temp
						| tex

<Arg>					::= <SimpleArg>
						| Replicate '(' <SimpleArg> ')'
						| Complement '(' <SimpleArg> ')'

<Properties>			::= <Props Block>
						| <Texture Props Block>
						| <Props Block> <Texture Props Block>
						| <Texture Props Block> <Props Block>

<Props Block>			::= props '{' <PropsList> '}'

<PropsList>				::= <PropSingle>
						| <PropSingle> <PropsList>

<PropSingle>			::= <Prop Blend>
						| <Prop Depth>
						| <Prop Cull>
						| <Prop AlphaTest>
						| <Prop Specular>
						| <Prop Stencil>
						| <Prop TFactor>
						| <Prop Clipping>
						| <Prop Lighting>
						| <Prop Ambient>

<Cmp Func>				::= <Cmp Func Cmp>
						| <Cmp Func NoCmp>

<Cmp Func Cmp>			::= Less
						| LessEqual
						| Greater
						| GreaterEqual
						| Equal
						| NotEqual

<Cmp Func NoCmp>		::= Never
						| Always

<Prop Ambient>			::= ambient '=' <Int Val> ';'

<Prop Lighting>			::= lighting '=' <Bool Val> ';'

<Prop Clipping>			::= clipping '=' <Bool Val> ';'

<Prop TFactor>			::= tfactor = <Int Val> ';'

<Prop Stencil>			::= stencil '=' <Stencil Args> do '(' <Stencil Actions> ')' ';'
						| stencil '=' <Stencil Args> ';'

<Stencil Args>			::= <Cmp Func Cmp> '(' <Stencil Ref Val> ',' <Stencil Mask> ')'
						| <Cmp Func Cmp> '(' <Stencil Ref Val> ')'
						| <Cmp Func NoCmp>
						| None

<Stencil Ref Val>		::= DecimalLiteral
						| HexLiteral

<Stencil Mask>			::= DecimalLiteral
						| HexLiteral

<Stencil Actions>		::= <Stencil Action> ',' <Stencil Action> ',' <Stencil Action>

<Stencil Action>		::= Keep
						| Zero
						| Replace
						| Inc
						| Dec
						| IncSat
						| DecSat
						| Invert

<Prop Specular>			::= specular '=' <Bool Val> ';'

<Prop AlphaTest>		::= alphatest '=' <AlphaTest Vals> ';'

<AlphaTest Vals>		::= None
						| <Cmp Func Cmp> '(' <Alpha Ref> ')'
						| <Cmp Func NoCmp>
			
<Alpha Ref>				::= <Int Val>

<Prop Cull>				::= cull '=' <Cull Mode> ';'

<Cull Mode>				::= None
						| CW
						| CCW

<Prop Depth>			::= depth '=' <Depth Func> ';'

<Depth Func>			::= None
						| NoWrite
						| <Cmp Func>

<Prop Blend>			::= blend '=' <Blend Op> '(' <Src Blend> ',' <Dst Blend> ')' ';'

<Blend Op>				::= Add
						| Sub
						| RevSub
						| Min
						| Max

<Src Blend>				::= <Blend Modes>

<Dst Blend>				::= <Blend Modes>

<Blend Modes>			::= Zero
						| One
						| SrcColor
						| InvSrcColor
						| SrcAlpha
						| InvSrcAlpha
						| DstColor
						| InvDstColor
						| DstAlpha
						| InvDstAlpha
						| SrcAlphaSat

<Texture Props Block>	::= <TexStageProps>
						| <TexStageProps> <Texture Props Block>

<TexStageProps>			::= stageprops '(' <Int Val> ')' '{' <TexPropList> '}'

<TexPropList>			::= <TexSingleProp>
						| <TexSingleProp> <TexPropList>

<TexSingleProp>			::= address '(' <TexWrapMode> ',' <TexWrapMode> ',' <TexWrapMode> ')' ';'
						| address '(' <TexWrapMode> ',' <TexWrapMode> ')' ';'
						| filter '(' <TexFilterMode> ',' <TexFilterMode> ',' <TexFilterMode> ')' ';'
						| filter '(' <TexFilterMode> ',' <TexFilterMode> ')' ';'

<TexWrapMode>			::= Wrap
						| Mirror
						| Clamp
						| Border
						| MirrorOnce

<TexFilterMode>			::= None
						| Point
						| Linear
						| Anisotropic
						| FlatCubic
						| GaussianCubic

<Int Val>				::= HexLiteral
						| DecimalLiteral

<Bool Val>				::= true
						| false

